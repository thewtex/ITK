if(NOT ITK_BUILD_DEFAULT_MODULES)
  message(FATAL_ERROR "BUILD_EXAMPLES requires ITK_BUILD_DEFAULT_MODULES to be ON")
endif()

find_package(ITK REQUIRED)
include(${ITK_USE_FILE})

set(ITK_TEST_DRIVER itkTestDriver)

# add_example(ExampleName [ExampleNameSource.cxx])
macro(add_example example_name)
  if(${ARGC} GREATER 1)
    add_executable(${example_name} ${ARGV1})
  else()
    add_executable(${example_name} ${example_name}.cxx)
  endif()
  target_link_libraries(${example_name} ${ITK_LIBRARIES})
  if(ITK_USE_COTIRE)
    cotire(${example_name})
  endif()
endmacro()

add_subdirectory(DataRepresentation)
add_subdirectory(Filtering)
add_subdirectory(Installation)
add_subdirectory(IO)
add_subdirectory(Iterators)
add_subdirectory(Numerics)
add_subdirectory(RegistrationITKv4)
add_subdirectory(Segmentation)
add_subdirectory(SpatialObjects)
add_subdirectory(Statistics)

if(ITKV3_COMPATIBILITY)
  add_subdirectory(RegistrationITKv3)
endif()
